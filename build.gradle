buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'io.codearte.gradle.nexus', name: 'gradle-nexus-staging-plugin', version: '0.11.0'
    }
}

plugins {
    id 'groovy'
    id 'maven-publish'
    id 'jacoco'
    id 'nebula.optional-base' version '3.2.0'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'maven'
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.7.2'
    id 'io.codearte.nexus-staging' version '0.11.0'
}

scmVersion {
    tag {
        prefix = ''
    }
}

group 'com.github.mkopylec'
archivesBaseName = name
version scmVersion.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

ext.springBootVersion = '1.5.6.RELEASE'

dependencies {

    compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBootVersion
    compile("org.springframework.boot:spring-boot-starter-data-couchbase:$springBootVersion") {
        exclude group: 'org.springframework', module: 'spring-web'
    }
    compile group: 'org.springframework.session', name: 'spring-session', version: '1.3.1.RELEASE'
    compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.2.1.RELEASE'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.9'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'

    compile group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: springBootVersion, optional

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-undertow', version: springBootVersion
    testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.1-groovy-2.4'
}

configurations {
    all*.exclude group: 'org.mortbay.jetty', module: 'servlet-api'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourceJar
}

signing {
    if (project.ext.has('signArtifacts')) {
        sign configurations.archives
    }
}

ext {
    ossrhUsername = project.ext.has('ossrhUsername') ? project.ext.ossrhUsername : ''
    ossrhPassword = project.ext.has('ossrhPassword') ? project.ext.ossrhPassword : ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name 'Session Couchbase Spring Boot Starter'
                packaging 'jar'
                description 'Spring Boot starter for Couchbase backed HTTP session'
                url 'https://github.com/mkopylec/session-couchbase-spring-boot-starter'
                scm {
                    connection 'scm:git:https://github.com/mkopylec/session-couchbase-spring-boot-starter.git'
                    developerConnection 'scm:git:https://github.com/mkopylec/session-couchbase-spring-boot-starter.git'
                    url 'https://github.com/mkopylec/session-couchbase-spring-boot-starter'
                }
                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'mkopylec'
                        name 'Mariusz Kopylec'
                        email 'mariusz.kopylec@o2.pl'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
