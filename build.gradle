plugins {
    id 'groovy'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'maven'
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
    id 'io.codearte.nexus-staging' version '0.21.0'
}

scmVersion {
    tag {
        prefix = ''
    }
}

group 'com.github.mkopylec'
archivesBaseName = name
version scmVersion.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

ext.springBootVersion = '2.1.8.RELEASE'

dependencies {

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-couchbase', version: springBootVersion
    compile group: 'org.springframework.session', name: 'spring-session-core', version: '2.1.8.RELEASE'
    compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.2.4.RELEASE'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

    compileOnly group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: springBootVersion

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
    testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.3-groovy-2.5'
    testCompile group: 'org.testcontainers', name: 'couchbase', version: '1.12.4'
}

wrapper {
    gradleVersion = '6.0.1'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourceJar
}

signing {
    if (project.ext.has('signArtifacts')) {
        sign configurations.archives
    }
}

ext {
    ossrhUsername = project.ext.has('ossrhUsername') ? project.ext.ossrhUsername : ''
    ossrhPassword = project.ext.has('ossrhPassword') ? project.ext.ossrhPassword : ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name 'Session Couchbase Spring Boot Starter'
                packaging 'jar'
                description 'Spring Boot starter for Couchbase backed HTTP session'
                url 'https://github.com/mkopylec/session-couchbase-spring-boot-starter'
                scm {
                    connection 'scm:git:https://github.com/mkopylec/session-couchbase-spring-boot-starter.git'
                    developerConnection 'scm:git:https://github.com/mkopylec/session-couchbase-spring-boot-starter.git'
                    url 'https://github.com/mkopylec/session-couchbase-spring-boot-starter'
                }
                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'mkopylec'
                        name 'Mariusz Kopylec'
                        email 'mariusz.kopylec@o2.pl'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
